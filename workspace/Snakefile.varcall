import snkmk
configfile: "config.yml"
VARCALL_REGIONS = snkmk.make_regions(config["refs"], window=config["varcall"]["chunksize"])
CHROMOSOMES = snkmk.make_chroms(config["refs"])
RUNLIB2SAMP, SAMP2RUNLIB = snkmk.make_runlib2samp()
SAMPLESETS = snkmk.make_samplesets(config['sample_sets'])

rule all_samples_only:
    input:
        expand("data/variants/filtered/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf",
               caller=config["varcall"]["callers"],
               aligner=config["varcall"]["aligners"],
               ref=config["varcall"]["genomes"],
               sampleset="all_samples",
               filter=config["varcall"]["filters"]),

rule everything:
    input:
        expand("data/variants/filtered/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf",
               caller=config["varcall"]["callers"],
               aligner=config["varcall"]["aligners"],
               ref=config["varcall"]["genomes"],
               sampleset=SAMPLESETS,
               filter=config["varcall"]["filters"]),


rule freebayes:
    input:
        bam="data/alignments/{aligner}/{ref}/sets/{sampleset}.bam",
        ref=lambda wc: config['refs'][wc.ref],
    output:
        bcf=temp("data/variants/raw_split/freebayes/{aligner}/{ref}/{sampleset}/{region}.bcf"),
        idx=temp("data/variants/raw_split/freebayes/{aligner}/{ref}/{sampleset}/{region}.bcf.csi"),
    log:
        "data/log/freebayes/{aligner}/{ref}/{sampleset}/{region}.log"
    threads: 1
    params:
        region=lambda wc: "' --region '".join(VARCALL_REGIONS[wc.ref][wc.region]),
        theta=config["varcall"].get("theta_prior", 0.01),
    shell:
        "( freebayes"
        "   --theta {params.theta}"
        "   --use-reference-allele"
        "   --min-mapping-quality 10"
        "   --min-base-quality 10"
        "   --use-mapping-quality"
        "   --genotype-qualities"
        "   --region '{params.region}'"
        "   --fasta-reference {input.ref}"
        "   {input.bam}"
        " | bcftools view"
        "   -O b"
        "   -o {output.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule mpileup:
    input:
        bams=lambda wc: expand("data/alignments/{aligner}/{ref}/{sample}.bam", aligner=wc.aligner,
                               ref=wc.ref, sample=SAMPLESETS[wc.sampleset]),
        bamlist="data/bamlists/{aligner}/{ref}/{sampleset}.bamlist",
        ref=lambda wc: config['refs'][wc.ref],
    output:
        bcf=temp("data/variants/raw_split/mpileup/{aligner}/{ref}/{sampleset}/{region}.bcf"),
        idx=temp("data/variants/raw_split/mpileup/{aligner}/{ref}/{sampleset}/{region}.bcf.csi"),
    log:
        "data/log/mpileup/{aligner}/{ref}/{sampleset}/{region}.log"
    threads: 1
    params:
        region=lambda wc: "' --region '".join(VARCALL_REGIONS[wc.ref][wc.region]),
        targets=lambda wc: "' --targets '".join(VARCALL_REGIONS[wc.ref][wc.region]), # for bcftools
        theta=config["varcall"].get("theta_prior", 0.01),
    shell:
        "( samtools mpileup"
        "   --output-tags DP,AD,ADF,ADR,SP,INFO/AD,INFO/ADF,INFO/ADR" #output everything
        "   --region '{params.region}'"
        "   --fasta-ref {input.ref}"
        "   --bam-list {input.bamlist}"
        "   --redo-BAQ"
        "   --BCF --uncompressed"
        " | bcftools call"
        "   --targets '{params.targets}'" # might not be needed
        "   --multiallelic-caller"
        "   --prior {params.theta}"
        "   -O b"
        "   -o {output.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule bcfmerge:
    input:
        bcf=lambda wc: expand("data/variants/raw_split/{caller}/{aligner}/{ref}/{sampleset}/{region}.bcf",
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset,
                              region=sorted(VARCALL_REGIONS[wc.ref])),
        idx=lambda wc: expand("data/variants/raw_split/{caller}/{aligner}/{ref}/{sampleset}/{region}.bcf.csi",
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset,
                              region=sorted(VARCALL_REGIONS[wc.ref])),
    output:
        bcf="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf",
        idx="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf.csi",
    log:
        "data/log/mergebcf/{caller}/{aligner}/{ref}/{sampleset}.log"
    threads: 2
    shell:
        "( bcftools concat"
        "   --allow-overlaps"
        "   --remove-duplicates"
        "   --threads {threads}"
        "   -O b"
        "   -o {output.bcf}"
        "   {input.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule bcffilter:
    input:
        bcf="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf",
        idx="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf.csi",
    output:
        bcf="data/variants/filtered/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf",
        idx="data/variants/filtered/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf.csi",
    log:
        "data/log/bcffilter/{caller}/{aligner}/{ref}/{sampleset}/{filter}.log"
    threads: 2
    params:
        filtarg=lambda wc: config["varcall"]["filters"][wc.filter].replace('\n', ' ')
    shell:
        "( bcftools view"
        "   --threads {threads}"
        "   {params.filtarg}"
        "   -O b"
        "   -o {output.bcf}"
        "   {input.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

