configfile: "config.yml"
import snkmk
RUNLIB2SAMP, SAMP2RUNLIB = snkmk.make_runlib2samp()
SAMPLESETS = snkmk.make_samplesets(config['sample_sets'])
shell.prefix = "set -euo pipefail; "
VARCALL_REGIONS = snkmk.make_regions(config["refs"], window=config["varcall"]["chunksize"])

wildcard_constraints:
    run="[^/]+",
    lib="[^/]+",
    aligner="[^/]+",
    sample="[^/]+",
    ref="[^/]+",
    type="[^/]+",

subworkflow align:
    workdir: ".."
    snakefile: "align.rules"


rule freebayes:
    input:
        bam=align("data/alignments/{aligner}/{ref}/sets/{sampleset}.bam"),
        ref=lambda wc: config['refs'][wc.ref],
    output:
        bcf=temp("data/variants/raw_split/freebayes/{aligner}/{ref}/{sampleset}/{region}.bcf"),
        idx=temp("data/variants/raw_split/freebayes/{aligner}/{ref}/{sampleset}/{region}.bcf.csi"),
    log:
        "data/log/freebayes/{aligner}/{ref}/{sampleset}/{region}.log"
    threads: 1
    params:
        region=lambda wc: "' --region '".join(VARCALL_REGIONS[wc.ref][wc.region]),
        theta=config["varcall"].get("theta_prior", 0.01),
    shell:
        "( freebayes"
        "   --theta {params.theta}"
        "   --use-reference-allele"
        "   --min-mapping-quality 10"
        "   --min-base-quality 10"
        "   --use-mapping-quality"
        "   --genotype-qualities"
        "   --region '{params.region}'"
        "   --fasta-reference {input.ref}"
        "   {input.bam}"
        " | bcftools view"
        "   -O b  -o {output.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule mpileup:
    input:
        bams=lambda wc: align(expand("data/alignments/{aligner}/{ref}/samples/{sample}.bam", aligner=wc.aligner,
                               ref=wc.ref, sample=SAMPLESETS[wc.sampleset])),
        bamlist=align("data/bamlists/{aligner}/{ref}/{sampleset}.bamlist"),
        ref=lambda wc: config['refs'][wc.ref],
    output:
        bcf=temp("data/variants/raw_split/mpileup/{aligner}/{ref}/{sampleset}/{region}.bcf"),
        idx=temp("data/variants/raw_split/mpileup/{aligner}/{ref}/{sampleset}/{region}.bcf.csi"),
    log:
        "data/log/mpileup/{aligner}/{ref}/{sampleset}/{region}.log"
    threads: 1
    params:
        region=lambda wc: "' --region '".join(VARCALL_REGIONS[wc.ref][wc.region]),
        targets=lambda wc: "' --targets '".join(VARCALL_REGIONS[wc.ref][wc.region]), # for bcftools
        theta=config["varcall"].get("theta_prior", 0.01),
    shell:
        "( samtools mpileup"
        "   --output-tags DP,AD,ADF,ADR,SP,INFO/AD,INFO/ADF,INFO/ADR" #output everything
        "   --region '{params.region}'"
        "   --fasta-ref {input.ref}"
        "   --bam-list {input.bamlist}"
        "   --redo-BAQ"
        "   --BCF --uncompressed"
        " | bcftools call"
        "   --targets '{params.targets}'" # might not be needed
        "   --multiallelic-caller"
        "   --prior {params.theta}"
        "   -O b"
        "   -o {output.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule bcfmerge:
    input:
        bcf=lambda wc: expand("data/variants/raw_split/{caller}/{aligner}/{ref}/{sampleset}/{region}.bcf",
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset,
                              region=sorted(VARCALL_REGIONS[wc.ref])),
        idx=lambda wc: expand("data/variants/raw_split/{caller}/{aligner}/{ref}/{sampleset}/{region}.bcf.csi",
                              caller=wc.caller, aligner=wc.aligner, ref=wc.ref, sampleset=wc.sampleset,
                              region=sorted(VARCALL_REGIONS[wc.ref])),
    output:
        bcf="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf",
        idx="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf.csi",
    log:
        "data/log/mergebcf/{caller}/{aligner}/{ref}/{sampleset}.log"
    threads: 2
    shell:
        "( bcftools concat"
        #"   --allow-overlaps" # These two options make bcftools extremely slow, with no noticble difference
        #"   --remove-duplicates"
        "   --threads {threads}"
        "   -O b"
        "   -o {output.bcf}"
        "   {input.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule bcfnorm:
    input:
        bcf="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf",
        idx="data/variants/raw/{caller}/{aligner}/{ref}/{sampleset}.bcf.csi",
    output:
        bcf="data/variants/normalised/{caller}~{aligner}~{ref}~{sampleset}.bcf",
        idx="data/variants/normalised/{caller}~{aligner}~{ref}~{sampleset}.bcf.csi",
    log:
        "data/log/bcfnormalise/{caller}/{aligner}/{ref}/{sampleset}.log"
    threads: 1
    shell:
        "( bcftools norm"
        "   --rm-dup any"
        "   --fasta-ref {input.ref}"
        "   -O u"
        "   {input.bcf}"
        " | vt decompose_blocksub + -o -" # decompose MNP to multipe SNPs
        " | bcftools norm" # second norm is to ensure that the decompose didn't lead to un-normed variants
        "   --rm-dup any"
        "   --fasta-ref {input.ref}"
        "   --multiallelics +any" # Merge overlapping bi-allelics to one multi-allelic
        "   -O b  -o {output.bcf}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule bcffilter:
    input:
        bcf="data/variants/normalised/{caller}~{aligner}~{ref}~{sampleset}.bcf",
        idx="data/variants/normalised/{caller}~{aligner}~{ref}~{sampleset}.bcf.csi",
    output:
        bcf="data/variants/filtered/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf",
        idx="data/variants/filtered/{caller}~{aligner}~{ref}~{sampleset}~filtered-{filter}.bcf.csi",
    log:
        "data/log/bcffilter/{caller}/{aligner}/{ref}/{sampleset}/{filter}.log"
    threads: 1
    params:
        filtarg=lambda wc: config["varcall"]["filters"][wc.filter].replace('\n', ' ')
    shell:
        "( bcftools view"
        "   --threads {threads}"
        "   {params.filtarg}"
        " && bcftools index -f {output.bcf}"
        " ) >{log} 2>&1"

rule bcf2vcf:
    input:
        bcf="{path}.bcf",
        idx="{path}.bcf.csi",
    output:
        vcf="{path}.vcf.gz",
        idx="{path}.vcf.gz.csi",
    log:
        "data/log/bcf2vcf/{path}.log"
    threads: 1
    shell:
        "( bcftools view"
        "   -O v"
        "   {input.bcf}"
        " | bgzip > {output.vcf}"
        " && bcftools index {output.vcf}"
        " ) >{log} 2>&1"

